#
# Spreadsheet.itk
#
# jeff.hobbs@acm.org, Copyright (c) 1997 Jeffrey Hobbs, CADIX International
#
# version 1.0
#

##
## This quick hack is not a fully developed megawidget, but an interesting
## starting point.  I'd welcome any improvements (I'm just lazy).
##

# Default resources.
option add *Spreadsheet.borderWidth 1 widgetDefault
option add *Spreadsheet.relief sunken widgetDefault
option add *Spreadsheet.vscrollMode static widgetDefault
option add *Spreadsheet.hscrollMode static widgetDefault

# Usual options.  This stuff is ridiculously complex.
itk::usual Spreadsheet {
    keep -foreground -background -font -borderwidth \
	    -insertbackground -insertborderwidth -insertwidth \
	    -insertontime -insertofftime \
	    -highlightcolor -highlightthickness
    rename -highlightbackground -background background Background
    # add more options here
}

# ------------------------------------------------------------------
#                           SPREADSHEET
# ------------------------------------------------------------------
class Spreadsheet {
    inherit itk::Widget

    constructor {args} {}
    destructor {}

    itk_option define -sbwidth sbWidth Width 12
    itk_option define -vscrollmode vscrollMode ScrollMode static
    itk_option define -hscrollmode hscrollMode ScrollMode static

    private method _resize {} 
    public method print {}

    protected variable arrayName
}

#
# Provide a lowercased access method for the Spreadsheet class.
# 
proc ::spreadsheet {pathName args} {
    uplevel ::Spreadsheet $pathName $args
}

body Spreadsheet::constructor {args} {
    set arrayName $this
    regsub -all "::" $arrayName "_" arrayName
    private variable $arrayName

    itk_component add entryfield {
	entryfield $itk_interior.entry \
		-labelvariable [scope ${arrayName}(current)] -labeltext "0,0" \
		-textvariable [scope ${arrayName}(active)]
    } {
	keep -background -cursor -foreground
    }
    pack $itk_component(entryfield) -fill x

    itk_component add tableframe {
	frame $itk_interior.frame
    } {
	keep -background -cursor
    }
    pack $itk_component(tableframe) -fill both -expand 1

    itk_component add table {
	table $itk_component(tableframe).table -variable [scope ${arrayName}] \
		-browsecommand [list set [scope ${arrayName}(current)] %S] \
		-titlerows 1 -titlecols 1 \
		-xscrollcommand [code $itk_component(tableframe).horizsb set] \
		-yscrollcommand [code $itk_component(tableframe).vertsb set]
    } {
	keep -anchor -background -borderwidth -cursor -exportselection \
		-font -foreground -highlightcolor -highlightthickness \
		-insertbackground -insertborderwidth -insertofftime \
		-insertontime -insertwidth -padx -pady -relief -takefocus \
		-autoclear -batchmode -colorigin -cols -colseparator \
		-colstretchmode -coltagcommand -command -drawmode \
		-flashmode -flashtime -maxheight -maxwidth \
		-roworigin -rows -rowseparator -rowstretchmode -rowtagcommand \
		-selectioncommand -selectmode -selecttype -state \
		-titlerows -titlecols -usecommand -validate -validatecommand

	rename -highlightbackground -background background Background
	#rename -height -rowheight rowHeight RowHeight
	#rename -width -colwidth colWidth ColWidth
    }
    itk_component add vertsb {
	scrollbar $itk_component(tableframe).vertsb -orient vertical \
		-command [code $itk_component(table) yview]
    } {
	keep -activebackground -activerelief -background -borderwidth \
		-cursor -elementborderwidth \
		-highlightcolor -jump -highlightthickness -relief \
		-repeatdelay -repeatinterval -troughcolor
	rename -highlightbackground -background background Background
    }
    itk_component add horizsb {
	scrollbar $itk_component(tableframe).horizsb -orient horizontal \
		-command [code $itk_component(table) xview]
    } {
	keep -activebackground -activerelief -background -borderwidth \
		-cursor -elementborderwidth \
		-highlightcolor -jump -highlightthickness -relief \
		-repeatdelay -repeatinterval -troughcolor
	rename -highlightbackground -background background Background
    }
    grid $itk_component(table) $itk_component(vertsb) -sticky news
    grid $itk_component(horizsb) -sticky ew
    grid columnconfig $itk_component(tableframe) 0 -weight 1
    grid rowconfig $itk_component(tableframe) 0 -weight 1

    # set table parameters
    eval itk_initialize $args

    bind $itk_component(table) <Configure> [code $this _resize]
}

body Spreadsheet::destructor {} { catch {unset $arrayName} }

body Spreadsheet::print {} {
    global $arrayName
    parray $arrayName
}

# ------------------------------------------------------------------
# OPTION: -sbwidth
#
# Set the width of the scrollbars.
# ------------------------------------------------------------------
configbody Spreadsheet::sbwidth {
    $itk_component(vertsb) configure -width $itk_option(-sbwidth)
    $itk_component(horizsb) configure -width $itk_option(-sbwidth)
}

# ------------------------------------------------------------------
# OPTION: -vscrollmode
#
# Enable/disable display and mode of veritcal scrollbar.
# ------------------------------------------------------------------
configbody Spreadsheet::vscrollmode {
    switch $itk_option(-vscrollmode) {
	static	{ grid $itk_component(vertsb) }
	dynamic	-
	none	{ grid remove $itk_component(vertsb) }
	default {
	    error "bad vscrollmode option\
		    \"$itk_option(-vscrollmode)\": should\
		    be static, dynamic, or none"
	}
    }
}

# ------------------------------------------------------------------
# OPTION: -hscrollmode
#
# Enable/disable display and mode of horizontal scrollbars.
# ------------------------------------------------------------------
configbody Spreadsheet::hscrollmode {
    switch $itk_option(-hscrollmode) {
	static	{ grid $itk_component(horizsb) }
	dynamic	-
	none	{ grid remove $itk_component(horizsb) }
	default {
	    error "bad hscrollmode option\
		    \"$itk_option(-hscrollmode)\": should\
		    be static, dynamic, or none"
	}
    }
}

# ------------------------------------------------------------------
# PRIVATE METHOD: _resize
#
# ------------------------------------------------------------------
body Spreadsheet::_resize {} {
    if {[string comp {0 1} [$itk_component(table) xview]]} {
	grid $itk_component(horizsb)
    } else {
	grid remove $itk_component(horizsb)
    }
    if {[string comp {0 1} [$itk_component(table) yview]]} {
	grid $itk_component(vertsb)
    } else {
	grid remove $itk_component(vertsb)
    }
}

