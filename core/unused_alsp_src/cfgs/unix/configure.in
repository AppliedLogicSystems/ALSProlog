dnl Process this file with autoconf to produce a configure script.
dnl
dnl
AC_INIT(generic/wintcode.c)

arch_mach_os=`${srcdir}/config.guess`
echo
echo Configuring for $arch_mach_os ...
echo

ARCH=`echo $arch_mach_os | sed -e 's/-.*//'`
SOS=`echo $arch_mach_os | sed -e 's/.*-//' -e 's/\..*$//'`
OS=`echo $arch_mach_os | sed -e 's/.*-//'`

AC_SUBST(ARCH)
AC_SUBST(OS)
AC_SUBST(SOS)
AC_DEFINE(MinorOSStr,\"$OS\")

###	checks for alternative programs
AC_LN_S
AC_PROG_CC
AC_PROG_CPP
AC_GCC_TRADITIONAL
AC_PROG_INSTALL

###	checks for UNIX variants that set C preprocessor variables
AC_AIX
AC_ISC_POSIX
AC_MINIX
AC_DYNIX_SEQ
AC_IRIX_SUN

###	checks for header files
AC_STDC_HEADERS
AC_DIR_HEADER
AC_XENIX_DIR
AC_HAVE_HEADERS(fcntl.h ldfcn.h libc.h libgen.h \
		machine/param.h memory.h netdb.h regex.h \
		select.h sgtty.h stdarg.h stddef.h stdlib.h string.h \
		sys/mman.h sys/param.h sys/select.h sys/socket.h sys/stat.h \
		sys/time.h sys/times.h sys/wait.h \
		termios.h termio.h ucontext.h unistd.h)

###	checks for typedefs
AC_RETSIGTYPE
AC_DEFINE(SIGT,RETSIGTYPE)
case "$DEFS" in
    *RETSIGTYPE=void* ) AC_DEFINE(SIGRET,[/*do nothing*/]) ;;
    * ) AC_DEFINE(SIGRET,[return 0]) ;;
esac

AC_MODE_T
AC_OFF_T
AC_PID_T
AC_SIZE_T
AC_UID_T

###	checks for library functions
AC_SETVBUF_REVERSED
dnl The following doesn't define HAVE_MMAP for sunos!
dnl AC_MMAP

###	checks for structures
AC_STAT_MACROS_BROKEN
AC_ST_BLKSIZE
AC_ST_BLOCKS
AC_ST_RDEV
AC_TIME_WITH_SYS_TIME
AC_STRUCT_TM

###	checks for compiler characteristics
AC_CONST

###	checks for system services and user specified options
AC_LONG_FILE_NAMES
AC_RESTARTABLE_SYSCALLS
VC_HAVE_LIBRARY(dl)
case "$DEFS" in
	*HAVE_LIBDL* ) ;;
	* ) VC_HAVE_LIBRARY(ld) ;;
esac

VC_HAVE_LIBRARY(PW)
if test "$ARCH" = m68k; then
    VC_HAVE_LIBRARY(m881)
    case "$DEFS" in
    	*HAVE_LIBM881* ) AC_DEFINE(MATH_RETVAL_IN_FP) ;;
	* ) VC_HAVE_LIBRARY(m) ;;
    esac
else
    VC_HAVE_LIBRARY(m)
fi
VC_HAVE_LIBRARY(gen)		# needed on some systems for regular expressions

# The reason for the following is this:  The libgen.a library on M88k SVR3
# (Delta) machines contain a definition for waitpid which seems to be buggy.
# We need to have a library with a non-buggy version of waitpid first on the
# link line in order to not pick up this buggy version.  libc.a has non-buggy
# versions of wait and waitpid, so we make sure that -lc appears ahead of
# -lgen.

case "$DEFS" in
	*HAVE_LIBGEN* ) VC_HAVE_LIBRARY(c) ;;
	* ) ;;
esac

VC_HAVE_LIBRARY(nsl)		# needed on some systems to find gethostname
VC_HAVE_LIBRARY(socket)

AC_SUBST(X_CFLAGS)
AC_SUBST(X_LIBS)
AC_SUBST(X_EXTRA_LIBS)

AC_DEFINE(UNIX)

AC_FIND_XTRA
# GNU autoconf-1.9 does not give me everything I need for
# X_CFLAGS.  The following test will (hopefully) find the 
# additional flags needed to make it through X11/Xos.h.
AC_CHECKING([for additional compiler flags needed with X11])
if mkdir conftestdir; then
  cd conftestdir
  cat > Imakefile <<'EOF'
findstddefs:
	@echo 'im_std_defines="${STD_DEFINES}"'
EOF
  if (xmkmf) >/dev/null 2>/dev/null && test -f Makefile; then
    # GNU make sometimes prints "make[1]: Entering...", which
    # would confuse us.
    eval `make findstddefs 2>/dev/null | grep -v make`
    X_CFLAGS="$X_CFLAGS $im_std_defines"
  fi
  cd ..
  rm -rf conftestdir
fi

VC_SETPGRP
AC_HAVE_FUNCS(aint bcopy dlopen exp10 \
	gamma getcwd gethostname getpagesize getwd \
	lgamma memmove mkdir mmap mprotect poll \
	re_comp regcmp rexec rint \
	select setitimer setsid sigaction sigaltstack sigstack sigvec \
	socket srand srand48 srandom \
	strcspn strdup strspn strtok \
	symlink sysconf \
	tempnam time times \
	vfprintf vm_allocate vm_protect \
	wait3 waitpid)

###	checks for missing headers
VC_MISSING_EXTERN( \
_filbuf		\
_flsbuf		\
accept		\
access		\
alarm		\
atoi		\
bcopy		\
bind		\
brk		\
bzero		\
chdir		\
close		\
connect		\
dup		\
dup2		\
execl		\
fclose		\
fflush		\
fork		\
fprintf		\
fputc		\
fread		\
free		\
fseek		\
fwrite		\
getcwd		\
getenv		\
gethostname	\
getpagesize	\
getpgrp		\
getpid		\
getsockname	\
getuid		\
getwd		\
kill		\
ldopen		\
listen		\
longjmp		\
lseek		\
malloc		\
memmove		\
memset		\
mkdir		\
mprotect	\
open		\
perror		\
pipe		\
printf		\
read		\
readlink	\
realloc		\
recvfrom	\
regcmp		\
regex		\
re_comp		\
re_exec		\
rewind		\
rexec		\
rmdir		\
sbrk		\
select		\
sendto		\
setitimer	\
setpgrp		\
setvbuf		\
shutdown	\
sigstack	\
sigvec		\
sleep		\
socket		\
stat		\
symlink		\
system		\
time		\
times		\
unlink		\
vfprintf	\
vsprintf	\
wait		\
write		\
)

###	output Makefile and config.h
changequote({,})dnl
AC_OUTPUT(Makefile bld-port/makefile bld-port/tconfig.h bld-natv/makefile bld-natv/tconfig.h config.d,
{
echo creating config.h
sed	-e '/^# /d' \
	-e 's/ -D/\
#define /g' \
	-e 's/\(#define [A-Za-z_][A-Za-z0-9_]*\)=/\1	/g' \
	config.d >config.h
}
)
changequote([,])dnl
